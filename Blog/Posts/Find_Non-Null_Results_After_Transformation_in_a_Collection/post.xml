tag:blogger.com,1999:blog-6671019398434141469.post-32184819919090783222011-11-07T06:53:00.000+01:002011-11-07T06:53:24.728+01:00Groovy Goodness: Find Non-Null Results After Transformation in a Collection<p>Since Groovy 1.8.1 we can use the <code>findResults</code> method and pass a closure to transform elements in a collection and get all non-null elements after transformation. We also have the <a href="http://mrhaki.blogspot.com/2010/12/groovy-goodness-find-first-non-null.html"><code>findResult</code></a> method to return the first non-null transformed element, but with <code>findResults</code> we get all non-null elements.</p><pre class="brush:groovy">def stuff = ['Groovy', 'Griffon', 'Gradle', 'Spock', 'Grails', 'GContracts']
def stuffResult = stuff.findResults { it.size() == 6 ? "$it has 6 characters" : null }

assert stuffResult == ['Groovy has 6 characters', 
                       'Gradle has 6 characters',
                       'Grails has 6 characters']
                        
def map = [what: 'Finish blog post', priority: 1, when: new Date()]
def mapResult = map.findResults { it.value instanceof String ? "Key $it.key is of type String" : null }

assert mapResult == ['Key what is of type String']
</pre>Hubert Klein Ikkinkhttps://plus.google.com/116304108087638220732noreply@blogger.com0